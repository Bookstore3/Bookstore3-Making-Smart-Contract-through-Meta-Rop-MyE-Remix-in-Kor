# 1장
### 소스 코드 간단 분석

---

먼저 스마트 컨트랙트를 배포하기 앞서, 배포를 할 컨트랙트 코드를 살펴보겠다.
지금은 코드를 따라가면서 무엇을 하는 코드인지에 대한 이해와 진행의 흐름을 이해하는 시간이다.
실제로 코드를 이용해 스마트 컨트랙트를 배포하는 것은 2장에서부터 다룰것이다.

~~~Solidity
pragma solidity ^0.4.24; //컴파일러 버전 지정

contract SmartController {

    // Controller가 사용할 구조체
    struct Controller {
    address addr;  // client의 주소
    uint endTime; // 이용 종료 시간
    bool status;  // true일 때만 사용가능
    }

    address public owner; // 오너의 주소 (EOA)
    address public iot; // IoT Device의 주소 (CA)

    mapping (uint => Controller) public controllers; // Controller 구조체를 담을 매핑
    uint public numPaid; //결제 횟수 카운터 변수

    /// 오너의 권한 체크
    modifier onlyOwner() {
      require(msg.sender == owner);
      _;
    }
    /// IoT 장치 권한 체크
    modifier onlyIoT() {
      require(msg.sender == iot);
      _;
    }

    ///생성자, IoT장치의 주소를 매개변수로 받음
    constructor (address _iot) public { // 접근지정자 꼭!
          // owner의 값에 이 계약을 생성한 계정 주소 대입
      owner = msg.sender;
      iot = _iot;
      numPaid = 0;
    }

    //이더 지불 시, 호출되는 함수
    function payToController() public payable {
      require(msg.value == 1000000000000000000); //1ETH가 아니면 종료
      //Controller 생성
      Controller storage c = controllers[numPaid++]; //storage로 생성
      c.addr = msg.sender;
      c.endTime = now + 60; // 이용시간 1분, 원하는 시간 만큼 수정 가능
      c.status = true;
    }

    /// 상태(staus)를 변경하는 함수, 이용 종료 시각에 호출
    // controllers의 키 값이 매개변수가 됨
    function updateStatus(uint _index) public onlyIoT {
      //인덱스 값에 해당하는 Controller 구조체가 없으면 종료
      require(controllers[_index].addr != 0);

      //이용 종료 시간이 되지 않았으면 종료
      require(now > controllers[_index].endTime);

      controllers[_index].status = false; //상태변경
    }
    // 지불된 이더를 인출하는 함수
    function withdrawFunds() public onlyOwner {
       require (owner.send(address(this).balance));
    }
    // 계약을 소멸시키는 함수
    function kill() public onlyOwner {
      selfdestruct(owner);
    }
}
//made by Heuristic Wave
//add를 addr로 표기 변경
~~~

코드에 달린 주석을 쭉 살펴보고 앞에서 미디엄 링크를 타고 들어가 글을 봤다면 어느정도 감이 잡혔겠지만, 이 컨트랙트는 IoT 기기를 스마트 컨트랙트를 통해 빌리는 간단한 코드를 작성한 것이다.

물론 매우 간단하게만 작성을 하였고, 입문자용 이기에 완벽한 코드는 아니다.
어느 부분이 완벽하지 않은지는 아래 컨트랙트 흐름을 설명하며 같이 첨부하겠다.

**참조:** 코드를 분석하기 앞서 몇가지 알아두어야 할 것들이 있다.
- modifier
~~~Solidity
/// 오너의 권한 체크
modifier onlyOwner() {
  require(msg.sender == owner);
  _;
}
/// IoT 장치 권한 체크
modifier onlyIoT() {
  require(msg.sender == iot);
  _;
}
~~~
이 코드를 보면 modifier라는 키워드를 볼 수 있다. 이는 강력한 if 문을 모듈화 시켰다고 생각을 하면 된다.
modifier 문 안에 \_;라는 부분이 있는 것을 알 수 있을 것이다. 이 부분은 이 modifier 문을 if 처럼 사용하고 나서
처리를 할 코드가 붙게 되는 부분이라고 생각을 하면 된다.
예를 들어보겠다.
~~~Solidity
// 계약을 소멸시키는 함수
function kill() public onlyOwner {
  selfdestruct(owner);
}
~~~
이 코드는 배포한 스마트 컨트랙트를 죽이는 함수이다.
이런 코드를 실행시킬 수 있어야 하는 것은 컨트랙트를 배포한 owner만 할 수 있어야한다.
이를 위해 function kill() public **onlyOwner** 부분을 보면 뒤에 onlyOwner가 붙어있는 것을 볼 수 있다.
이를 통해 실제로 이 코드는
~~~Solidity
// 계약을 소멸시키는 함수
function kill() public {
  require(msg.sender == owner);

  selfdestruct(owner);
}
~~~
이런 형식으로 코드가 실행이 된다.
이렇게 if문을 강력하게 모듈화 시킴으로써, 스마트 컨트랙트를 작성할 때 매번 if문을 작성하는 번거로움을 덜 수 있다.

- Storage & Memory
코드를 보면 컨트랙트에 이더를 지불하는 아래와 같은 코드가 있다.
~~~Solidity
//이더 지불 시, 호출되는 함수
function payToController() public payable {
  require(msg.value == 1000000000000000000); //1ETH가 아니면 종료
  //Controller 생성
  Controller storage c = controllers[numPaid++]; //storage로 생성
  c.addr = msg.sender;
  c.endTime = now + 60; // 이용시간 1분, 원하는 시간 만큼 수정 가능
  c.status = true;
}
~~~
여기서 봐야 할 것은 Controller **storage** c = controllers[numPaid++];의 storage라는 부분이다.

컴퓨터에는 메모리와 하드 디스크와 같은 스토리지, 저장 공간이 있다.
메모리는 컴퓨터가 실행되는 동안에만 기억을 하고 있다가 컴퓨터가 꺼지면 다 지워지는 휘발성 공간이다.
이에 반해 스토리지는 컴퓨터가 꺼지더라도 저장 내용을 기억하고 있는 비휘발성 공간이다.

이처럼 스마트 컨트랙트를 처리하는 EVM (이더리움 가상 머신)에도 메모리와 스토리지 공간이 있다.
스마트 컨트랙트가 실행 동안에 잠시 저장하는 것들은 메모리(memory)에 저장을 하고,
스마트 컨트랙트가 실행되고 있지 않은 상태에서도 블록체인 상에 기록이 되어 계속 유지가 되어야 하는 정보들은
스토리지(storage)에 기억을 시켜야 한다.

위에 Controller storage c = controllers[numPaid++]; 코드는 IoT를 사용하는 사람들의 정보를 저장해야 하는 공간이기 때문에
다음번에 컨트랙트를 실행해도 사용자를 기억할 수 있게 storage로 기억을 하게 만든 것이다.

<br>

자 그럼 이제 본격적으로 코드의 분석 및 흐름을 이해해 보자.

이 컨트랙트에서는 총 3개의 요소가 서로 상호 작용을 하게 된다.

1. 컨트랙트를 배포한 owner
2. 컨트랙트에 등록될 IoT 기기
3. 컨트랙트를 통해 IoT 기기를 빌릴 고객(클라이언트)

이제 이 3개 요소들의 상호작용의 흐름을 정리해 보자면

<br>

1. 먼저 owner가 컨트랙트를 배포(deploy)를 한다.

   배포를 한다는 것은 즉, 이 코드를 생성한다는 것으로 생성자를 호출하게 될 것이다.
   ~~~Solidity
   constructor (address _iot) public {
     owner = msg.sender;
     iot = _iot;
     numPaid = 0;
   }
   ~~~
   생성자에는 메세지를 보낸 주체의 주소, 즉 컨트랙트를 생성한 주체의 주소가 msg.sender로 들어와 owner에 입력이 된다.
   그리고 보는 것 처럼 생성자에는 address \_iot 라는 매개변수가 있는 것을 알 수 있을 것이다. 그럼 배포를 할 때, 이 스마트 컨트랙트에 등록될 IoT 기기의 주소(계정)을 입력을 하고 배포를 한다.
   마지막으로는 이 컨트랙트에 돈을 보낸 주체들을 관리하기 위한 인덱스 번호로서 쓰일 numPaid를 0으로 초기화 하고 생성자가 완료가 된다.

   >**참조:** 솔리디티 컴파일러 버전이 올라감에 따라 원래는 function이라는 함수 명으로 생성자를 선언했으나, 생성자를 선언할 때 컨트랙트 이름을 입력하는 도중 오류가 일어나는 경우가 많아 아예 이름을 쓸 필요가 없게 constructor 키워드로 생성자를 선언하게 변경되었다.

<br>

2. 그럼 이제 스마트 컨트랙트의 배포가 되었으니 IoT 기기를 빌리고 싶은 클라이언트가 스마트  컨트랙트에 1 이더를 보낼 것이다.

   이더를 지불하기 위해 payToController() 함수를 호출한다.
   ~~~Solidity
   //이더 지불 시, 호출되는 함수
   function payToController() public payable {
     require(msg.value == 1000000000000000000); //1ETH가 아니면 종료
     //Controller 생성
     Controller storage c = controllers[numPaid++]; //storage로 생성
     c.addr = msg.sender;
     c.endTime = now + 60; // 이용시간 1분, 원하는 시간 만큼 수정 가능
     c.status = true;
   }
   // 상태(staus)를 변경하는 함수, 이용 종료 시각에 호출
   ~~~
   먼저 클라이언트가 보낸 메세지(msg)에 이더값(value)를 체크해 1이더가 되는지 확인을 한다. (1 Eth = 10^18 Wei)
   만약 1이더가 되었다는 것을 확인을 하면 이제 storage 상에 Controller 구조체를 담을 mapping인 controllers를 저장을 한다.
   이번이 처음으로 지불된 경우이므로 numPaid는 0번째로서 controllers[0]에 이번 구조체가 담길 것이며, 다음번 클라이언트가 지불을 할 때를 위해
   numPaid++를 통해 numPaid를 1로 만들어 놓는다.

   여기서 잠시 코드상으로 상황을 봐보면, 같은 클라이언트가 본인이 IoT 사용 시간이 다 끝나지 않더라도 계속해서 1 이더를 보내면 계속해서
   controllers[0], controllers[1], controllers[2].. 하고 controllers가 생성될 것이다.
   즉, 꼭 0번째와 1번째의 클라이언트가 달라야 하는 것이 아닌, 말 그대로 돈을 보낸 것을 기준으로 클라이언트 상태를 관리를 하는 것이다.

   물론 0번째가 사용하고 있는 동안에는 다른 사람들이 이더를 지불할 수 없게 만들어야 완벽한 0번째 클라이언트의 대여가 될 것이다. 그러나 앞에서 말한 것처럼
   이번 글은 스마트 컨트랙트를 배포 및 이용해 보는 것이 목적인 글이다. 잘 짜여진 솔리디티 코드들은 많은 오픈소스 코드들을 참조하며 공부를 하는 것을 추천한다.

   그럼 다시 payToController()로 돌아와서, 클라이언트가 1이더를 지불했고, 그 지불에 대해 controllers[0]에 Controller 구조체를 할당하고
   이제 그 구조체 안에 돈을 보낸 클라이언트의 주소, 이용 종료 시간 그리고 IoT 대여 여부 상태인 status 값을 true로 설정을 한다.
   ~~~Solidity
   struct Controller {
   address addr; // client의 주소
   uint endTime; // 이용 종료 시간
   bool status; // true일 때만 사용가능
   }
   ~~~
   여기서 알아두어야 할 점은 **endTime = now + 60** 에서 **now** 라는 값이다. 이 now라는 값은 자칫 Current Time으로서 볼 수 있지만 사실 이 now는 클라이언트의 트랜잭션이 들어간 블록의 timeStamp 값이다. 즉, 추후에 만들어진 블록의 timeStamp값이 현재 값 보다 60만큼 크게 설정이 되어야 endTime의 조건이 성사가 되는 것이다.

   여기서 만약 다음번 블록이 60초 보다 빠르게 생성이 되었다면, 다음번 블록이 생성이 될 때 까지 기다려야 timeStamp 값이 endTime 값보다 커지게 되어 이용시간이 종료되었다는 것을
   알 수 있을 것이다.

   만약 네트워크가 블록을 더이상 생성하지 않는다면 새로운 timeStamp를 가진 블록이 생성이 되지 않을 것이기 때문에 클라이언트가 무한대로 IoT기기를 사용할 수 있게 될 것이다.

   예를 들어, 만약 클라이언트가 우리가 만든 컨트랙트에 1 이더를 지불하고,
   그 트랜색션이 1번 블록에 기록이 되었다고 하자. 그리고 그 1번 블록의 timeStamp 값이 1000이였다고 가정해보자.
   그럼 이 클라이언트의 endTime은 1060으로 설정이 될 것이다.

   그런데 2번째 블록이 생성이 되지 않고 네트워크가 블록 생성을 중단해 버린다면 endTime은 영원히 오지 않고 계속해서 now의 값은 1000으로 유지가 될 것이다.

   설령 2번째 블록이 생성이 되었더라도 2번째 블록의 timeStamp가 1030으로 생성이 되었다면 3번째 블록이 생성이 될 때 까지 기다려야 한다. 그리고 그 3번째 블록의 timeStamp가
   1060이거나 그 이상의 값이 나와야 나중에 endTime이 초과가 되었는지 (이용시간이 끝났는지) 확인할 수 있을 것이다.

   다시 한번 말하지만, 완벽한 코드를 위한 글이 아닌 스마트 컨트랙트 배포 및 이용이 목적인 글이다.

<br>

3. 클라이언트의 endTime, 즉 이용 종료 시간이 되었다면 **IoT 기기** 가 클라이언트의 이용 상태 (status)를 false로 변경을 시킨다.
   ~~~Solidity
   function updateStatus(uint _index) public onlyIoT {
     //인덱스 값에 해당하는 Controller 구조체가 없으면 종료
     require(controllers[_index].addr != 0);

     //이용 종료 시간이 되지 않았으면 종료
     require(now > controllers[_index].endTime);

     controllers[_index].status = false; //상태변경
   }
   ~~~
   코드를 보면, 해당 클라이언트의 번호를 매개변수로 받고, 그 번호에 클라이언트의 주소가 있는지 먼저 확인을 한다.
   만약 해당 인덱스에 클라이언트의 주소가 있다면 이더를 지불한 클라이언트가 있다는 것이 확인이 된다.

   클라이언트가 있다는 것을 확인했다면 이제 **now > controllers[_index].endTime** 을 통해 클라이언트의 사용 종료 시간보다 now가 더 높은지,
   즉, 이용 시간이 초과가 되었는지 확인을 한다.
   > 위에서도 말한 것처럼 now는 현재 시간이 아니다. 블록의 timeStamp 시간이다. 그러나 간단한 코드를 위해 now라는 변수를 사용했다는 점 기억하길 바란다.

   만약 이용 시간이 초과가 되었다면 그 클라이언트의 status를 false로 변경을 해, 클라이언트가 IoT 기기를 사용할 수 없게 만든다.

   여기서 중요한 점은 이 updateStatus()를 호출하는 주체는 IoT 기기(onlyIoT) 라는 점이다.
   IoT를 사용하는 클라이언트의 이용시간이 끝나면 이용을 종료시키는 것은 클라이언트가 할 일이 아니라 IoT가 할 일이기 때문이다.
   원래라면 Listener 등을 달아주어 시간을 계속 체크해야 하지만 일단 그런 부분은 구현하지 않았다.

<br>

4. 마지막으로 클라이언트들이 이렇게 이용을 하고, 이용을 종료하는 상호작용을 진행하면, 이 컨트랙트에 계속해서 이더가 쌓일 것이다.
   그럼 이제 이 컨트랙트를 배포한 사람이자 IoT 기기의 주인일 owner가 이 컨트랙트에 쌓인 이더를 회수를 할 차례이다.
   이는 withdrawFunds()를 통해 진행한다.
   ~~~Solidity
   function withdrawFunds() public onlyOwner {
      require (owner.send(address(this).balance));
   }
   ~~~
   withdrawFunds()를 통해 먼저 onlyOwner로 owner인지 확인을 한 후에, 지금 이 주소(address(this)) 즉, 컨트랙트 주소에 묶여있는
   돈(balance)를 owner한테 보낸다. 이를 통해 컨트랙트를 생성한 주체에게 이더가 전송이 된다.
   >send라는 함수보다는 transfer라는 함수를 사용하는 것을 권장한다고 한다.

<br>

   그럼 지금까지 전체적인 컨트랙트의 흐름을 다시 한번 간결히 정리를 하면 아래와 같다.

   1. **owner** 가 IoT 주소를 넣어 컨트랙트를 배포한다.
   2. 배포된 컨트랙트에 **클라이언트** 가 1이더를 보내 IoT 기기를 대여한다.
   3. 이용 시간이 초과가 되었으면 **IoT 기기** 가 클라이언트 대여 상태를 false로 만든다.
   4. **owner** 가 컨트랙트에 묶여있는 이더를 인출해 간다.

   이제 코드의 분석과 흐름에 대한 이해가 되었다면 이 코드를 이용해 스마트 컨트랙트를 배포해 보자.
